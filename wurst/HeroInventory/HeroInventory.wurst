//TODO unhock triggers / block execution to prevent activation on trigger action
//TODO abstract inventory state with inheritance
//TEMEMBER Item class and level useable
// MAybe: stacksizes
//TODO allow appropirate action in states (if in quip allow pickup equip)
// block actions while backpack
// addItem triggers pickup, remove triggers drop
// move triggers moveto and pickup
package HeroInventory
    import InventoryItems
    import Unit
    import Unit2
    import UnitIndexer2
    import Player2

    int ITEM_BELT = 'I004'
    int ITEM_EQUIP = 'I005'
    int ITEM_BACK = 'I003'

    HeroInventory array heroInventories

    enum InventoryState
        Belt
        Equip
        Back

    init
        CreateTrigger()
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_USE_ITEM)
            ..addAction(function switchState)
        CreateTrigger()
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER)
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER)
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER)
            ..addAction(function trackItemUsage)
        CreateTrigger()
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DROP_ITEM)
            ..addAction(function dropItem)
        CreateTrigger()
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM)
            ..addAction(function pickupItem)
        CreateTrigger()
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER)
            ..addAction(function pickupItemOrder)
        CreateTrigger()
            ..registerTimerEvent(1.0, false)
            ..addAction(function index)


    // Tracks item movement and usage across the slots
    function trackItemUsage()
        int id = GetIssuedOrderId()
        if id < 852001 or id > 852014
            return
        unit u = GetTriggerUnit()
        HeroInventory heroInv = HeroInventory.getHeroInventory(u)
        if heroInv == null
            return
        // Move item in inventory 852002-7
        for int i = 0 to 5
            int newId = 852002 + i
            if id == newId
                heroInv.moveItemToSlotOrder(i)
                return
        // Use item 852008-13
        for int i = 0 to 5
            int newId = 852008 + i
            if id == newId
                heroInv.useItemOrder(i)
                return

    function dropItem()
        unit u = GetTriggerUnit()
        HeroInventory heroInv = HeroInventory.getHeroInventory(u)
        if heroInv == null
            return
        heroInv.dropItemAction(GetManipulatedItem())

    function pickupItem()
        unit u = GetTriggerUnit()
        HeroInventory heroInv = HeroInventory.getHeroInventory(u)
        if heroInv == null
            return
        heroInv.pickupItemAction(GetManipulatedItem())

    function pickupItemOrder()
        item i = GetOrderTargetItem()
        if i == null
            return
        unit u = GetTriggerUnit()
        HeroInventory heroInv = HeroInventory.getHeroInventory(u)
        if heroInv == null
            return
        heroInv.pickupItemOrder(i)

    function switchState()
        unit u = GetTriggerUnit()
        HeroInventory heroInv = HeroInventory.getHeroInventory(u)
        if heroInv == null
            return
        int itt = GetManipulatedItem().getTypeId()
        if itt == ITEM_BELT
            heroInv.switchToBelt()
        else if itt == ITEM_EQUIP
            heroInv.switchToEquip()
        else if itt == ITEM_BACK
            heroInv.switchToBack()

    function index()
        group g = GetUnitsInRectAll(GetPlayableMapRect())
        for unit u in g
            if u.isHero()
                HeroInventory.createHeroInventory(u)
        g.destr()
            
    class HeroInventory
        private unit hero
        private InventoryState state = InventoryState.Belt
        private InventoryItems beltItems
        private InventoryItems equipItems
        private InventoryItems backItems

        static function createHeroInventory(unit u)
            HeroInventory hi = new HeroInventory(u)
            heroInventories[u.getIndex()] = hi

        static function getHeroInventory(unit u) returns HeroInventory
            return heroInventories[u.getIndex()]

        private construct(unit u)
            hero = u
            u.addItemInSlot(ITEM_BACK, 4)
            u.addItemInSlot(ITEM_EQUIP, 5)
            beltItems = new InventoryItems()
            beltItems.setItemInSlot(ITEM_BACK, 4)
            beltItems.setItemInSlot(ITEM_EQUIP, 5)
            equipItems = new InventoryItems()
            equipItems.setItemInSlot(ITEM_BELT, 4)
            backItems = new InventoryItems()
            backItems.setItemInSlot(ITEM_BELT, 5)

        function useItemOrder(int fromSlot)
            // Prevent item usage in equipment
            if state  == InventoryState.Equip and fromSlot != SLOT_BOTTOM_LEFT
                hero.abortOrder()
                hero.getOwner().simulateError("You cannot use items from your equipment")
                return
            // Prevent item usage in backpack
            if state  == InventoryState.Back and fromSlot != SLOT_BOTTOM_RIGHT
                hero.abortOrder()
                hero.getOwner().simulateError("You cannot use items from your backpack")
                return
    
        function moveItemToSlotOrder(int toSlot)
            InventoryItems oldState = new InventoryItems()
            oldState.saveFromUnit(hero)
            item toItm = hero.getItemInSlot(toSlot)
            int toItmType = toItm.getTypeId()
            toItm.remove()
            TriggerSleepAction(0)
            InventoryItems newState = new InventoryItems()
            newState.saveFromUnit(hero)
            item fromItm = hero.getItemInSlot(toSlot)
            int fromItmType = fromItm.getTypeId()
            fromItm.remove()
            int fromSlot = oldState.findMissingItem(fromItmType, newState)
            destroy oldState
            destroy newState
            // Prevent moving of switch items
            if fromItmType == ITEM_BELT or fromItmType == ITEM_EQUIP or fromItmType == ITEM_BACK
                hero.addItemInSlot(toItmType, toSlot)
                hero.addItemInSlot(fromItmType, fromSlot)
                return
            // Move item to equip
            if toItmType == ITEM_EQUIP
                //TODO tryEquip
                if (equipItems.addItem(fromItmType) == -1)
                    hero.addItemInSlot(fromItmType, fromSlot)
                hero.addItemInSlot(toItmType, toSlot)
                print("TODO Equip call")
                return
            // Move item to backpack
            if toItmType == ITEM_BACK
                if (backItems.addItem(fromItmType) == -1)
                    hero.addItemInSlot(fromItmType, fromSlot)
                    hero.getOwner().simulateError("Your backpack is full")
                hero.addItemInSlot(toItmType, toSlot)
                return
            // Move item to belt
            if toItmType == ITEM_BELT
                if (beltItems.addItem(fromItmType) == -1)
                    hero.addItemInSlot(fromItmType, fromSlot)
                    hero.getOwner().simulateError("Your belt is full")
                if state == InventoryState.Equip
                    print("TODO Unequip call")
                hero.addItemInSlot(toItmType, toSlot)
                return
            // Default action, switch items
            hero.addItemInSlot(toItmType, fromSlot)
            hero.addItemInSlot(fromItmType, toSlot)
    
        function dropItemAction(item itm)
            skip
    
        function pickupItemAction(item itm)
            skip
    
        function pickupItemOrder(item itm)
            if state == InventoryState.Equip
                hero.abortOrder()
                hero.getOwner().simulateError("You cannot pickup items from your equipment")

        function switchToBelt()
            saveCurrentInventoryItems()
            clearInventory()
            beltItems.fillIntoUnit(hero)
            state = InventoryState.Belt

        function switchToEquip()
            saveCurrentInventoryItems()
            clearInventory()
            equipItems.fillIntoUnit(hero)
            state = InventoryState.Equip

        function switchToBack()
            saveCurrentInventoryItems()
            clearInventory()
            backItems.fillIntoUnit(hero)
            state = InventoryState.Back

        private function saveCurrentInventoryItems()
            if state == InventoryState.Belt
                beltItems.saveFromUnit(hero)
            else if state == InventoryState.Equip
                equipItems.saveFromUnit(hero)
            else if state == InventoryState.Back
                backItems.saveFromUnit(hero)

        //TODO move into invItems
        private function clearInventory()
            for i = 0 to 5
                hero.dropItemInSlot(i).remove()